#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПриЗаписи")
Процедура RA_AdditionalFieldsПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если RA_AdditionalFields_ItsCostCentre Тогда
		RA_AdditionalFields_СоздатьПроверитьНазначение();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура RA_AdditionalFields_СоздатьПроверитьНазначение()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Назначения.Ссылка
	|ИЗ
	|	Справочник.Назначения КАК Назначения
	|ГДЕ
	|	Назначения.RA_AdditionalFields_CostCentre = &RA_AdditionalFields_CostCentre";

	Запрос.УстановитьПараметр("RA_AdditionalFields_CostCentre", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		НазначениеОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
	Иначе
		НазначениеОбъект = Справочники.Назначения.СоздатьЭлемент();	
	КонецЕсли;
	
	НазначениеОбъект.Наименование = Наименование;
	НазначениеОбъект.RA_AdditionalFields_CostCentre = Ссылка;
	НазначениеОбъект.ДвиженияПоСкладскимРегистрам = Истина;
	НазначениеОбъект.ТипНазначения = Перечисления.ТипыНазначений.Собственное;
	НазначениеОбъект.ТипОбъектаНазначения = Перечисления.ТипыОбъектовНазначений.Переработчик;
	НазначениеОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли