
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
Если Параметры.Свойство("АдресВХранилище") Тогда

	//@skip-check unknown-form-parameter-access
	Производители.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище));
	
КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если Модифицированность И СохранитьПараметры Тогда

		АдресВХранилищеПроизводители = ПолучитьАдресВоВРеменномХранилище();

		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("АдресВХранилищеПроизводители", АдресВХранилищеПроизводители);

		ОповеститьОВыборе(СтруктураПараметров);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПроизводители

&НаКлиенте
Процедура ПроизводителиПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьПараметры = Истина;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьАдресВоВРеменномХранилище()

	ТаблицаПроизводителей = Производители.Выгрузить();
	ТаблицаПроизводителей.Свернуть("Производитель");

	Возврат ПоместитьВоВременноеХранилище(ТаблицаПроизводителей);

КонецФункции

#КонецОбласти	